1 '          DOCTOR, Vers. 2.1
2 CLEAR 1000

10 '         Supervisor
20 GOSUB 90     ' Initialization
30 GOSUB 160    ' Input (and associated functions)
40 GOSUB 250    ' Swap word for opposite form
50 GOSUB 410    ' Keyword processing
60 GOSUB 910    ' Marker remover 
70 GOSUB 970    ' Doctor's reply
80 GOTO 30

90 '        Initialization
100 NM = 28
110 NU = 82
120 RANDOM
130 CLS: PRINT: PRINT
140 PRINT "PLEASE STATE YOUR PROBLEM."
150 RETURN

160 '       Input (and associated functions)
170 RESTORE
180 X = 0: PA$ = ""
190 INPUT PA$
200 IF PA$ = "" THEN PRINT "DO YOU HAVE A PROBLEM?": X = 1: GOTO 240
210 IF PA$ = PR$ THEN PRINT "PLEASE DON'T REPEAT YOURSELF!": X = 1: GOTO 240
220 PR$ = PA$
230 PA$ = " " + LEFT$(PA$, LEN(PA$) - 1) + " "
240 RETURN

250 '       Swap word for opposite form
260 IF X = 1 THEN 400
270 FOR I = 1 TO NM/2
280 READ TE$, TN$
290 FOR PS = 1 TO LEN(PA$) - LEN(TE$) + 1
300 IF TE$ = MID$(PA$, PS, LEN(TE$)) THEN PA$ = LEFT$(PA$, PS - 1) + TN$ + MID$(PA$, PS + LEN(TE$))
310 DATA " MOM "," MOTHER "," DATA "," FATHER "
320 DATA " DREAMS "," DREAM "
330 DATA " I "," YOU@ "," YOU "," I "," ME "," YOU "
340 DATA " MY "," YOUR* "
350 DATA " YOUR "," MY "," MYSELF "," YOURSELF* "
360 DATA " YOURSELF "," MYSELF "
370 DATA " I'M "," YOU'RE* "," YOU'RE "," I'M "," AM "," ARE@ "
380 DATA " WERE "," WAS "
390 NEXT PS, I
400 RETURN
410 '       Keyword processing
420 '       A. Checking for keywords
430 IF X = 1 THEN 900
440 FOR I = 1 TO NU
450 READ KE$, JU
460 FOR PS = 1 TO LEN(PA$) - LEN(KE$) + 1
470 IF KE$ = MID$(PA$, PS, LEN(KE$)) THEN DR$ = MID$(PA$, PS + LEN(KE$)): IF DR$ <> "" THEN 500 ELSE 900
480 NEXT PS, I
490 GOTO 790

500 DR$ = LEFT$(DR$, LEN(DR$) - 1): GOTO 900 
510 DATA "COMPUTER",1,"MACHINE",1
520 DATA " NAME ",2,"ALIKE",3," LIKE ",3," SAME ",3
530 DATA "YOU@ REMEMBER",4,"DO I REMEMBER",5,"YOU@ DREAMED ",6  
540 DATA " DREAM ",7," IF ",8,"EVERYBODY",9,"EVERYONE",9
550 DATA "NOBODY",9,"NO ONE",9,"WAS YOU@",10,"YOU@ WAS",11
560 DATA "WAS I",12,"YOUR* MOTHER",13,"YOUR* FATHER",13
570 DATA "YOUR* SISTER",13,"YOUR* BROTHER",13,"YOUR* WIFE",13
580 DATA "YOUR* HUSBAND",13,"YOUR* CHILDREN",13,"YOUR*",14
590 DATA "ALWAYS",15,"ARE I",16,"ARE@ YOU@",18," HOW ", 25
600 DATA "BECAUSE",19,"CAN I",20,"CAN YOU@",21,"CERTAINLY",22
610 DATA "DEUTSCH",23,"ESPANOL",23,"FRANCAIS",23,"HELLO",24
620 DATA "I REMIND YOU OF",3,"I ARE",26,"I'M",26
630 DATA "ITALIANO",23,"MAYBE",28," MY ",29," NO ",30
640 DATA "PERHAPS",28,"SORRY",31,"WHAT ",25,"WHEN ",25
650 DATA "WHY DON'T I",32,"WHY CAN'T YOU@",33,"YES",22
660 DATA "YOU@ WANT",34,"YOU@ NEED",34," ARE ",17," I ",27
670 DATA "YOU@ ARE@ SAD",35,"YOU'RE* SAD",35
680 DATA "YOU@ ARE@ UNHAPPY",35,"YOU'RE* UNHAPPY",35
690 DATA "YOU@ ARE@ DEPRESSED",35,"YOU'RE* DEPRESSED",35
700 DATA "YOU@ ARE@ SICK",35,"YOU'RE* SICK",35
710 DATA "YOU@ ARE@ HAPPY",36,"YOU'RE* HAPPY",36
720 DATA "YOU@ ARE@ ELATED",36,"YOU'RE* ELATED",36
730 DATA "YOU@ ARE@ GLAD",36,"YOU'RE* GLAD",36
740 DATA "YOU@ ARE@ BETTER",36,"YOU'RE* BETTER",36
750 DATA "YOU@ FEEL YOU@",37,"YOU@ THINK YOU@",37
760 DATA "YOU@ BELIEVE YOU@",37,"YOU@ WISH YOU@",37
770 DATA " YOU@ ARE@",38,"YOU'RE*",38,"YOU@ CAN'T",39
780 DATA "YOU@ CANNOT",39,"YOU@ DON'T",40,"YOU@ FEEL",41
790 '           B. No keywords found
800 IF YO$ = "" THEN 810 ELSE RAN = RND(5): ON RAN GOTO 810, 810, 810, 860, 860
810 RAN = RND(4): ON RAN GOTO 820, 830, 840, 850
820 PRINT "I AM NOT SURE I UNDERSTAND YOU FULLY.": X = 1: GOTO 900
830 PRINT "PLEASE GO ON.": X = 1: GOTO 900
840 PRINT "WHAT DOES THAT SUGGEST TO YOU?": X = 1: GOTO 900
850 PRINT "DO YOU FEEL STRONGLY ABOUT DISCUSSING SUCH THINGS?": X = 1: GOTO 900
860 RAN = RND(3): ON RAN GOTO 870, 880, 890
870 PRINT "LET'S DISCUSS FURTHER WHY YOUR" + YO$ + ".": X = 1: GOTO 900
880 PRINT "EARLIER YOU SAID YOUR" + YO$ + ".": X = 1: GOTO 900
890 PRINT "DOES THAT HAVE ANYTHING TO DO WITH THE FACT THAT YOUR" + YO$ + "?": X = 1: GOTO 900
900 RETURN
910 '       Marker remover
920 IF X = 1 THEN 960
930 FOR PS = 1 TO LEN(DR$)
940 IF MID$(DR$, PS, 1) = "@" OR MID$(DR$, PS, 1) = "*" THEN DR$ = LEFT$(DR$, PS - 1) + MID$(DR$, PS + 1)
950 NEXT PS
960 RETURN

970 '       Doctor's reply
980 '       A. Line to jump to
990 IF X = 1 THEN 1470
1000 ON JU GOTO 1020,1030,1040,1050,1060,1070,1080,1090,1100,1110,1120,1130,1140,1150,1160,1170,1180,1190,1200,1210,1220,1230,1240,1250,1260,1270,1280,1290,1300,1310,1320,1370,1380,1390,1400,1410,1420,1430,1440,1450,1460
1010 '      B. Replies
1020 PRINT "DO COMPUTERS WORRY YOU?": GOTO 1470
1030 PRINT "I AM NOT INTERESTED IN NAMES.": GOTO 1470
1040 PRINT "IN WHAT WAY?": GOTO 1470
1050 PRINT "DO YOU OFTEN THINK OF" + DR$ + "?": GOTO 1470
1060 PRINT "DID YOU THINK I WOULD FORGET" + DR$ + "?": GOTO 1470
1070 PRINT "REALLY, " + DR$ + "?": GOTO 1470
1080 PRINT "WHAT DOES THAT DREAM SUGGEST TO YOU?": GOTO 1470
1090 PRINT "DO YOU THINK IT'S LIKELY THAT IF " + DR$ + "?": GOTO 1470
1100 PRINT "REALLY, " + DR$ + "?": GOTO 1470
1110 PRINT "WHAT IF YOU WERE" + DR$ + "?": GOTO 1470
1120 PRINT "WERE YOU REALLY?": GOTO 1470
1130 PRINT "WOULD YOU LIKE TO BELIEVE I WAS" + DR$ + "?": GOTO 1470
1140 PRINT "TELL ME MORE ABOUT YOUR FAMILY.": GOTO 1470
1150 PRINT "YOUR" + DR$ + ".": GOSUB 1480: GOTO 1470
1160 PRINT "CAN YOU THINK OF A SPECIFIC EXAMPLE?": GOTO 1470
1170 PRINT "WHY ARE YOU INTERESTED IN WHETHER I AM" + DR$ + " OR NOT?": GOTO 1470
1180 PRINT "DID YOU THINK THEY MIGHT NOT BE " + DR$ + "?": GOTO 1470
1190 PRINT "DO YOU BELIEVE YOU ARE" + DR$ + "?": GOTO 1470
1200 PRINT "IS THAT THE REAL REASON?": GOTO 1470
1210 PRINT "YOU BELIEVE I CAN" + DR$ + ", DON'T YOU?": GOTO 1470
1220 PRINT "WHETHER OR NOT YOU CAN" + DR$ + " DEPENDS ON YOU MORE THAN ON ME.": GOTO 1470
1230 PRINT "YOU SEEM QUITE POSITIVE.": GOTO 1470
1240 PRINT "I AM SORRY, I SPEAK ONLY ENGLISH.": GOTO 1470
1250 PRINT "HOW DO YOU DO.": GOTO 1470
1260 PRINT "WHY DO YOU ASK?": GOTO 1470
1270 PRINT "WHAT MAKES YOU THINK I AM" + DR$ + "?": GOTO 1470
1280 PRINT "WE WERE DISCUSSING YOU, NOT ME.": GOTO 1470
1290 PRINT "YOU DON'T SEEM QUITE CERTAIN.": GOTO 1470
1300 PRINT "WHY ARE YOU CONCERNED OVER MY " + DR$ + "?": GOTO 1470
1310 PRINT "ARE YOU SAYING 'NO' JUST TO BE NEGATIVE?": GOTO 1470
1320 RAN = RND(4): ON RAN GOTO 1330, 1340, 1350, 1360
1330 PRINT "PLEASE DON'T APOLOGIZE.": GOTO 1470
1340 PRINT "APOLOGIES ARE NOT NECESSARY.": GOTO 1470
1350 PRINT "WHAT FEELINGS DO YOU HAVE WHEN YOU APOLOGIZE?": GOTO 1470
1360 PRINT "YOU NEEDN'T FEEL THAT YOU HAVE TO APOLOGIZE.": GOTO 1470
1370 PRINT "DO YOU BELIEVE I DON'T" + DR$ + "?": GOTO 1470
1380 PRINT "DO YOU THINK YOU SHOULD BE ABLE TO" + DR$ + "?": GOTO 1470
1390 PRINT "WHAT WOULD IT MEAN TO YOU IF YOU GOT" + DR$ + "?": GOTO 1470
1400 GOSUB 1510: GOSUB 1540: PRINT "I AM SORRY TO HEAR YOU ARE" + DR$ + ".": GOTO 1470
1410 GOSUB 1510: GOSUB 1540: PRINT "HOW HAVE I HELPED YOU TO BE" + DR$ + "?": GOTO 1470
1420 PRINT "DO YOU REALLY THINK SO?": GOTO 1470
1430 PRINT "IS IT BECAUSE YOU ARE" + DR$ + " THAT YOU CAME TO ME?": GOTO 1470
1440 PRINT "HOW DO YOU KNOW YOU CAN'T" + DR$ + "?": GOTO 1470
1450 PRINT "DON'T YOU REALLY" + DR$ + "?": GOTO 1470
1460 PRINT "TELL ME MORE ABOUT SUCH FEELINGS.": GOTO 1470
1470 RETURN

1480 '      Special processing if keyword is MY
1490 IF LEN(DR$) > 11 THEN YO$ = DR$
1500 RETURN

1510 '      Remove "@" marker from key phrase if present
1520 IF MID$(KE$, 4, 1) = "@" THEN DR$ = RIGHT$(KE$, LEN(KE$) - 9)
1530 RETURN

1540 '      Remove "*" marker from key phrase if present
1550 IF MID$(KE$, 7, 1) = "*" THEN DR$ = RIGHT$(KE$, LEN(KE$) - 7)
1560 RETURN