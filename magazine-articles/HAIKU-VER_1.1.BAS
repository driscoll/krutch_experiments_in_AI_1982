1 '          HAIKU, VERS 1 1
2 '          BY JOHN KRUTCH
10 RANDOM
20 CLEAR 200
30 DEFSTR B,L,W
40 DEFINT A,I,N,P,R,Y
50 ARTNMB=4
60 ADJNMB=50
70 NOUNNMB=50
80 VERBNMB=13
90 PREPNMB=7
100 GOTO 140
110 LIN=""
120 RESTORE
130 A$=INKEY$:IF A$=CHR$(13) THEN 140 ELSE 130
140 CLS
150 RAN=RND(4)
160 ON RAN GOTO 400, 700, 1000, 1300
165 '          ARTICLE-CHECKING SUBROUTINE
166 '          THIS SUBROUTINE SCANS THROUGH THE LINE OF VERSE
167 '          JUST COMPOSED. IF IT FINDS "A" BEFORE A VOWEL,
168 '          OR "AN" BEFORE A CONSONANT, IT CONVERTS THE
169 '          ARTICLE TO ITS PROPER FORM
170 FOR I=1 TO LEN(LIN) - 2
180 IF MID$(LIN,I,3)=" A " THEN B=MID$(LIN,I+3,1):IF B="A" OR B="E" OR B="I" OR B="O" OR B="U" THEN LIN=LEFT$(LIN,I+1) + "N" + MID$(LIN,I+2)
190 NEXT I
200 FOR I=1 TO LEN(LIN) - 2
210 IF MID$(LIN,I,4)=" AN " THEN B=MID$(LIN,I+4,1):IF B<>"A" AND B<>"E" AND B<>"I" AND B<>"O" AND B<>"U" THEN LIN=LEFT$(LIN,I+1) + MID$(LIN,I+3)
220 NEXT I
230 RETURN
232 '          WORD-CHOOSING SUBROUTINE
233 '          THE VARIABLE P IS A POINTER WHICH IS SET TO THE
234 '          BEGINNING OF THE GROUP OF WORDS INDICATED BY
235 '          THE REQUIREMENTS OF THE PARTICULAR HAIKU
236 '          PATTERN IN USE. WITH THE HELP OF THE RANDOM
237 '          NUMBER RAN A WORD IS THEN CHOSEN AT RANDOM
238 '          FROM THIS GROUP.
240 RAN=RND(N) + P
250 FOR I=1 TO RAN
260 READ WRD
270 NEXT I
280 LIN=LIN + " " + WRD
290 RESTORE
300 RETURN
395 '          FIRST HAIKU PATTERN
400 N=ARTNMB: P=0: GOSUB 240 ' 240 IS WORD-CHOOSING SUBR
410 N=ADJNMB: P=ARTNMB: GOSUB 240
420 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
430 GOSUB 170 ' 170 IS ARTICLE-CHECKING SUBROUTINE
440 LIN=LIN + ","
450 PRINT@ 340, LIN
460 LIN=""
470 N=ARTNMB: P=0: GOSUB 240
480 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
490 N=VERBNMB: P=ARTNMB + ADJNMB + NOUNNMB: GOSUB 240
500 N=PREPNMB: P=ARTNMB + ADJNMB + NOUNNMB + VERBNMB: GOSUB 240
510 N=ARTNMB: P=0: GOSUB 240
520 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
530 GOSUB 170
540 PRINT@ 406, LIN
550 LIN=""
560 N=ADJNMB: P=ARTNMB: GOSUB 240
570 GOSUB 240
580 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
590 PRINT@ 472, LIN
600 GOTO 110
695 '         SECOND HAIKU PATTERN
700 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240 ' 240 IS WORD-CHOOSING SUBROUTI
710 N=PREPNMB: P=ARTNMB + ADJNMB + NOUNNMB + VERBNMB: GOSUB 240
720 N=ARTNMB: P=0: GOSUB 240
730 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
740 GOSUB 170 ' 170 IS ARTICLE-CHECKING SUBROUTINE
750 LIN=LIN + ","
760 PRINT@ 340, LIN
770 LIN=""
780 N=ARTNMB: P=0: GOSUB 240
790 N=ADJNMB: P=ARTNMB: GOSUB 240
800 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
810 N=PREPNMB: P=ARTNMB + ADJNMB + NOUNNMB + VERBNMB: GOSUB 240
820 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
830 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
840 GOSUB 170
850 PRINT@ 406, LIN
860 LIN=""
870 N=ADJNMB: P=ARTNMB: GOSUB 240
880 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
890 PRINT@ 472, LIN
900 GOTO 110
995 '         THIRD HAIKU PATTERN
1000 N=ARTNMB: P=0: GOSUB 240 ' 240 IS WORD-CHOOSING SUBR
1010 N=ADJNMB: P=ARTNMB: GOSUB 240
1020 GOSUB 240
1030 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
1040 GOSUB 170 ' 170 IS ARTICLE-CHECKING SUBR
1050 LIN=LIN + ","
1060 PRINT@ 340, LIN
1070 LIN=""
1080 N=PREPNMB: P=ARTNMB + ADJNMB + NOUNNMB + VERBNMB: GOSUB 240
1090 N=ARTNMB: P=0: GOSUB 240
1100 N=ADJNMB: P=ARTNMB: GOSUB 240
1110 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
1120 GOSUB 170
1130 PRINT@ 406, LIN
1140 LIN=""
1150 N=ARTNMB: P=0: GOSUB 240
1160 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
1170 N=VERBNMB: P=ARTNMB + ADJNMB + NOUNNMB: GOSUB 240
1180 GOSUB 170
1190 PRINT@ 472, LIN
1200 GOTO 110
1295 '         FOURTH HAIKU PATTERN
1300 N=ARTNMB: P=0: GOSUB 240 ' 240 IS WORD-CHOOSING SUBR
1310 N=ADJNMB: P=ARTNMB: GOSUB 240
1320 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
1330 N=VERBNMB: P=ARTNMB + ADJNMB + NOUNNMB: GOSUB 240
1340 GOSUB 170 ' 170 IS ARTICLE-CHECKING SUBROUTINE
1350 LIN=LIN + ","
1360 PRINT@ 340, LIN
1370 LIN=""
1380 N=ARTNMB: P=0: GOSUB 240
1390 N=ADJNMB: P=ARTNMB: GOSUB 240
1400 GOSUB 240
1410 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
1420 GOSUB 170
1430 PRINT@ 406, LIN
1440 LIN=""
1450 N=PREPNMB: P=ARTNMB+ADJNMB+NOUNNMB+VERBNMB:GOSUB 240
1460 N=ARTNMB: P=0: GOSUB 240
1470 N=ADJNMB: P=ARTNMB: GOSUB 240
1480 N=NOUNNMB: P=ARTNMB + ADJNMB: GOSUB 240
1490 GOSUB 170
1500 PRINT@ 472, LIN
1510 GOTO 110
2000 '          ARTICLES
2010 DATA "A","THE","AN","THE"
2100 '          ADJECTIVES
2110 DATA "AUTUMN","HIDDEN","BITTER","MISTY","SILENT","EMPTY"
2120 DATA "DRY","DARK","SUMMER","ICY","DELICATE","QUIET"
2130 DATA "WHITE","SUDDEN","COOL","SPRING","WINTER","DAPPLED"
2140 DATA "TWILIGHT","DAWN","CRIMSON","WISPY","AZURE"
2150 DATA "BLUE","BILLOWING","BROKEN","COLD","DAMP","FALLING"
2160 DATA "FROSTY","GREEN","LONG","LATE","LINGERING","LIMPID"
2170 DATA "LITTLE","MORNING","MUDDY","ORANGE","OLD","RED"
2180 DATA "STILL","SMALL","SPARKLING","THROBBING","WANDERING"
2190 DATA "WITHERED","WILD","BLACK","YOUNG"
2200 '          NOUNS
2210 DATA "SCARECROW","WATERFALL","RIVER","BREEZE","MOON"
2220 DATA "RAIN","WIND","SEA","MORNING","SNOW","LAKE","SUNSET"
2230 DATA "PINE","SHADOW","LEAF","DAWN","GLITTER","FOREST"
2240 DATA "HILL","CLOUD","MEADOW","SUN","GLADE","BIRD"
2250 DATA "BUTTERFLY","BUSH","CROW","DEW","DUST","FIELD"
2260 DATA "FLOWER","FIREFLY","FEATHER","GRASS","MOUNTAIN"
2270 DATA "NIGHT","POND","PINE-CONE","SHADE","SNOWFLAKE"
2280 DATA "SILENCE","SOUND","SKY","SHAPE","SURF","THUNDER"
2290 DATA "VIOLET","WATER","WILDFLOWER","WAVE"
2300 '          VERBS
2310 DATA "SHAKES","DRIFTS","HAS TURNED","STRUGGLES"
2320 DATA "HAS FALLEN","HAS PASSED","SLEEPS","CREEPS"
2330 DATA "FLUTTERS","HAS RISEN","IS FALLING","IS TRICKLING"
2340 DATA "IS FLOATING"
2400 '          PREPOSITIONS
2410 DATA "ON","IN","WITH","OF","THROUGH","BEHIND","UNDER"
2420 END