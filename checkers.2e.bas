1000 REM       CHECKERS.BAS
1010 REM-------------------------------
1100 REM       MAIN PROGRAM
1110 GOSUB 1300: REM INITIALIZATION
1120 GOSUB 1500: REM INITIALIZE ARRAY
1130 GOSUB 2800: REM COMPUTER 1ST MOVE
1140 GOSUB 1800: REM DRAW CHECKERBOARD
1150 GOSUB 2000: REM PRINT CURRENT
1160 GOSUB 2400: REM INPUT
1170 GOSUB 2500: REM CHANGE ARRAY
1180 GOSUB 2000: REM PRINT CURRENT
1190 IF M$="X" THEN 1160: REM D/T JUMPS
1200 GOSUB 2900: REM EVALUATION ROUTINE
1210 GOTO 1150
1220 REM-------------------------------
1300 REM       INITIALIZATION
1310 PRINT CHR$(147);
1320 Z = RND(-TI)
1330 DIM T$(11,11)
1340 BL$ = "@": WH$ = "@"
1350 KW$ = "@": ER$ = " "
1351 D1$ = CHR$(17)
1352 R1$ = CHR$(29)
1354 R3$ = R1$ + R1$ + R1$
1355 RT$ = R3$ + R3$ + R3$ + R1$
1356 RF$ = RT$ + RT$ + RT$ + RT$
1357 L1$ = CHR$(157)
1358 U1$ = CHR$(145)
1360 RETURN
1370 REM-------------------------------
1500 REM       INITIALIZE ARRAY
1510 FOR I = 2 TO 9
1520 :  FOR J = 2 TO 9
1530 :    T$(I,J) = ER$
1540 :  NEXT J
1550 NEXT I
1560 FOR I = 2 TO 4 STEP 2
1570 : FOR J = 2 TO 8 STEP 2
1580 :   T$(I,J) = WH$
1590 : NEXT J
1600 NEXT I
1610 FOR I = 3 TO 9 STEP 2
1620 : T$(3,I) = WH$
1630 NEXT I
1640 FOR I = 7 TO 9 STEP 2
1650 : FOR J = 3 TO 9 STEP 2
1660 :   T$(I,J) = BL$
1670 : NEXT J
1680 NEXT I
1690 FOR I = 2 TO 8 STEP 2
1700 : T$(8,I) = BL$
1710 NEXT I
1720 RETURN
1730 REM-------------------------------
1800 REM       DRAW CHECKERBOARD
1810 D$ = "@@@@@": L$ = "     "
1820 R$ = D$+L$+D$+L$+D$+L$+D$+L$
1830 S$ = L$+D$+L$+D$+L$+D$+L$+D$
1840 PRINT R$R$R$S$S$S$R$R$R$S$S$S$;
1850 PRINT R$R$R$S$S$S$R$R$R$S$S$S$;
1860 RETURN
1870 REM-------------------------------
2000 REM       PRINT CURRENT
2010 REM       ARRANGEMENT OF PIECES
2020 PRINT CHR$(19) R3$ R1$ R1$;
2030 FOR I = 9 TO 3 STEP -2
2040 : FOR J = 3 TO 9 STEP 2
2050 :   PRINT RIGHT$(STR$(I),1);
2060 :   PRINT RIGHT$(STR$(J),1);
2070 :   PRINT D1$ D1$ T$(I,J);
2080 :   PRINT U1$ U1$ R3$ R3$ R1$
2090 : NEXT J
2100 : PRINT RF$ RF$ RF$ RF$ RF$;
2110 NEXT I
2120 PRINT CHR$(19): PRINT D1$;
2130 FOR I = 8 TO 2 STEP -2
2140 : FOR J = 2 TO 8 STEP 2
2150 :   PRINT RIGHT$(STR$(I),1);
2160 :   PRINT RIGHT$(STR$(J),1);
2170 :   PRINT D1$ D1$ T$(I,J);
2180 :   PRINT U1$ U1$ R3$ R3$ R1$;
2190 : NEXT J
2200 : IF I = 2 THEN 2220
2210 : PRINT RF$ RF$ RF$ RF $ RF$;
2220 NEXT I
2230 RETURN
2240 REM-------------------------------
2400 REM       INPUT
2410 B$ = "":           REM NULL STRING
2420 GET A$: IF A$ = "" THEN 2420
2430 B$ = B$ + A$
2440 IF LEN(B$) <> 5 THEN 2420
2450 RETURN
2460 REM-------------------------------
2500 REM    CHANGE ARRAY TO REFLECT
2510 REM    NEW BOARD POSITION
2520 I = VAL(LEFT$(B$,1))
2530 J = VAL(MID$(B$,2,1))
2540 K = VAL(MID$(B$,4,1))
2550 L = VAL(RIGHT$(B$,1))
2560 M$ = MID$(B$,3,1)
2570 IF K=I-2 ANDL=J-2 THEN T$(K,L)=KW$:   T$(I,J)=ER$:T$(I-1,J-1)=ER$:G0T02650
2580 IF K=I-2 ANDL=J+2 THEN T$(K,L)=KW$:   T$(I,J)=ER$:T$(I-1,J+1)=ER$:G0T02650
2590 IF K=9 THEN T$(K,L)=KW$: GOTO 2620
2600 IF T$(I,J) = KW$ THEN T$(K,L) =KW$:     GOTO 2620
2610 T$(K,L) = WH$
2620 T$(I,J) = ER$
2630 IF K = I+2 AND L = J+2 THEN     T$(K-1,L-1) = ER$: GOTO 2650
2640 IF K = I+2 AND L = J-2 THEN     T$(K-1,L+1) = ER$
2650 RETURN
2660 REM-------------------------------
2800 REM       COMPUTER'S FIRST MOVE
2810 RA = INT(RND(1) * 4 + 1)
2820 IF RA = 1 THEN T$(7,3) = ER$:     T$(6,4) = BL$
2830 IF RA = 2 THEN T$(7,5) = ER$:     T$(6,4) = BL$
2840 IF RA = 3 THEN T$(7,5) = ER$:     T$(6,6) = BL$
2850 IF RA = 4 THEN T$(7,7) = ER$:     T$(6,6) = BL$
2860 RETURN
2870 REM-------------------------------
2900 REM       EVALUATION ROUTINE
2910 REM       A. BLACK LOOKING FOR
2920 REM          JUMP POSSIBILITIES
2930 FOR I = 2 TO 9
2940 FOR J = 2 TO 9
2950 IF T$(I,J) <> BL$ THEN 3000
2960 IF T$(I-1,J+1)=WH$ AND T$(I-2,J+2)=     ER$ THEN T$(I-1,J+1)=ER$:G0T0 2970
2965 GOTO 2980
2970 T$(I-2,J+2) = BL$: T$(I,J) = ER$:     GOTO 3320
2980 IF T$(I-1,J-1)=WH$ AND T$(I-2,J-2)=     ER$ THEN T$(I-1,J-1)=ER$:G0T0 2990
2985 GOTO 3000
2990 T$(I-2,J-2) = BL$: T$(I,J) = ER$:     GOTO 3320
3000 NEXT J
3010 NEXT I
3020 REM       B. BLACK GUARDING ITSELF
3030 REM          AGAINST A JUMP
3040 FOR I = 2 TO 9
3050 FOR J = 2 TO 9
3060 IF T$(I,J) <> BL$ THEN 3150
3070 IF T$(I-1,J-1)=WH$ AND T$(I+1,J+1)     = ER$ THEN 3110
3080 IF T$(I-1,J+1)=WH$ AND T$(I+1,J-1)     = ER$ THEN 3130
3090 IF T$(I-1,J+1) <> WH$ THEN 3150
3100 IF T$(I+1,J-1) <> ER$ THEN 3150
3110 IF T$(I+2,J) = BL$ THEN T$(I+2,J)=     ER$: T$(I+1,J+1) = BL$: GOTO 3320
3120 IF T$(I+2,J+2)=BL$ THEN T$(I+2,J+2)     = ER$: T$(I+1,J+1)=BL$: GOTO 3320
3130 IF T$(I+2,J-2)=BL$ THEN T$(I+2,J-2)     = ER$: T$(I+1,J-1)=BL$: GOTO 3320
3140 IF T$(I+2,J) = BL$ THEN T$(I+2,J)     = ER$: T$(I+1,J-1)=BL$: GOTO 3320
3150 NEXT J
3160 NEXT I
3170 REM       C. BLACK LOOKING FOR A
3180 REM          MOVE WHICH WON'T
3190 REM          RESULT IN A CAPTURE
3200 REM          OF BLACK'S MAN
3210 FOR I = 2 TO 9
3220 FOR J = 2 TO 9
3230 IF T$(I,J) <> BL$ THEN 3300
3240 IF T$(I-2,J)=WH$ AND T$(I,J-2)=ER$     THEN 3270
3250 IF T$(I-2,J-2) = WH$ THEN 3270
3260 IF T$(I-1,J-1) = ER$ THEN T$(I,J)=     ER$: T$(I-1,J-1) = BL$: GOTO 3320
3270 IF T$(I-2,J)=WH$ AND T$(I,J+2)=ER$     THEN 3300
3280 IF T$(I-2,J+2) = WH$ THEN 3300
3290 IF T$(I-1,J+1) = ER$ THEN T$(I,J)=     ER$: T$(I-1,J+1) = BL$: GOTO 3320
3300 NEXT J
3310 NEXT I
3311 REM       D. MAKE ANY OTHER MOVE
3312 FOR I = 2 TO 9
3313 FOR J = 2 TO 9
3314 IF T$(I,J) <> BL$ THEN 3317
3315 IF T$(I-1,J-1) = ER$ THEN T$(I,J)=     ER$: T$(I-1,J-1) = BL$: GOTO 3320
3316 IF T$(I-1,J+1) = ER$ THEN T$(I,J)=     ER$: T$(I-1,J+1) = BL$: GOTO 3320
3317 NEXT J
3318 NEXT I
3320 REM       SET ARRAY EDGES TO NULLS
3330 T$(0,0) = "": T$(0,1) = ""
3340 T$(1,0) = "": T$(1,1) = ""
3350 T$(10,10) = "": T$(10,11) = ""
3360 T$(11,10) = "": T$(11,11) = ""
3370 RETURN
3380 REM-------------------------------